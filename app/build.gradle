apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    useLibrary 'org.apache.http.legacy'
    defaultConfig {
        applicationId "com.example.nvmtech"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    dataBinding {
        enabled true
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    compileOptions {
        encoding "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "versionCode"
    productFlavors {
        development {
            getProps('./env/dev.props').each { p ->
                buildConfigField 'String', p.key, p.value
            }
        }
        production {
            getProps('./env/prod.props').each { p ->
                buildConfigField 'String', p.key, p.value
            }
        }
    }

    android.variantFilter { variant ->
        if (variant.buildType.name == 'release'
                && variant.getFlavors().get(0).name == 'development') {
            variant.setIgnore(true);
        }
    }
}

def getProps(path) {
    Properties props = new Properties()
    props.load(new FileInputStream(file(path)))
    return props
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // andorid support
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.intuit.sdp:sdp-android:1.0.6'

    // logger
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // extensions
    implementation "android.arch.lifecycle:extensions:1.1.1"

    //butter knife
    implementation 'com.jakewharton:butterknife:10.0.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.0.0'

    //room database
//    implementation 'android.arch.persistence.room:runtime:1.0.0'
//    annotationProcessor 'android.arch.persistence.room:compiler:1.0.0'

    //retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-simplexml:2.3.0'

    // testing
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'


}
